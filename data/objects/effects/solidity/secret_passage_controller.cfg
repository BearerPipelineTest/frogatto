{
id: "secret_passage_controller",
ignore_collide: true,
hidden_in_game: true,
is_strict: true,
zorder: "@include data/zorder.cfg:in_front_of_everything",

//This is needed, because on load of level we need to set solidity _everywhere_. Otherwise
//objects get trapped in the solid area that was a secret passage, and the engine asserts.
//See issue #534 for an example of this. <https://github.com/frogatto/frogatto/issues/534>
//A proper fix might be to have on_load always fire regardless of activation status.
always_active: true,


editor_info: {
	category: "controllers",
},

properties: {
	set_activation_area: "
		set(activation_area, [
			min(_x_bound,_x2_bound) -200,
			min(_y_bound,_y2_bound) -200,
			abs(_x_bound-_x2_bound) +200,
			abs(_y_bound-_y2_bound) +200
		])
	",
	
	install_secret_area: "
		if(should_exist, set_solid(_x_bound, _y_bound, _x2_bound, _y2_bound, _solid))
		where should_exist = if(
			level.player is obj frogatto_playable, 
			not level.player.player_has_died_since_last_unique_checkpoint, 
			false
		)
	",



#-------------------------- editor settings --------------------------#
	_x_bound: { type: "int", editor_info: { type: "x", value: "x-100" } },
	_x2_bound: { type: "int", editor_info: { type: "x", value: "x+150" } },
	_y_bound: { type: "int", editor_info: { type: "y", value: "y-100" } },
	_y2_bound: { type: "int", editor_info: { type: "y", value: "y+150" } },
	_solid: { type: "bool", editor_info: { type: "boolean", value: "false" }, default: false },
},


on_start_level: "set_activation_area",

on_load: "[set_activation_area, install_secret_area]",

on_level_tiles_refreshed: "install_secret_area",


on_end_anim: "animation('normal')",

animation: {
	id: "normal",
	image: "effects/particles.png",
	x: 179,
	y: 73,
	w: 28,
	h: 28,
	frames: 1,
	duration: 1000,
},
}
