//show how many hitpoints either a boss, or a collection of multiple enemies representing a boss, has
{
	id: "boss_progress_display",
	prototype: ["hud_component"],

	hidden_in_game: true,
	no_compile_image: "gui/progress-bar-center.png,gui/progress-bar-well-center.png",

	properties: {
		
		reset: "def() -> commands [
			//set((object<-level.gui[0].vars).bosses, []),                //Clear any saved bosses from the level. When the checkpoint is activated, it doesn't muck around with any of the level variables.
			map([widget]<-widgets.children, set(value.visible, false)), //Hide the display.
			set(last_displayed_value, 0.0),                             //And set the length to zero, so the bar starts off empty
		]",
		
//		reset_boss_list: "commands :: set(me.boss_list, [])",
		
//		boss_list: { type: "[obj hittable]", default: [] },









		hp_bar_center: {
			type: "obj boss_progress_display.bar_center",
			init: "object('boss_progress_display.bar_center', mid_x, mid_y, {parent: me})",
		},
		hp_bar_left: {
			type: "obj boss_progress_display.left_cap",
			init: "object('boss_progress_display.left_cap', mid_x, mid_y, {parent: me})",
		},
		hp_bar_right: {
			type: "obj boss_progress_display.right_cap",
			init: "object('boss_progress_display.right_cap', mid_x, mid_y, {parent: me})",
		},

		well_center: {
			type: "obj boss_progress_display.well_center",
			init: "object('boss_progress_display.well_center', mid_x, mid_y, {parent: me})",
		},
		well_left: {
			type: "obj boss_progress_display.well_left_cap",
			init: "object('boss_progress_display.well_left_cap', mid_x, mid_y, {parent: me})",
		},
		well_right: {
			type: "obj boss_progress_display.well_right_cap",
			init: "object('boss_progress_display.well_right_cap', mid_x, mid_y, {parent: me})",
		},
		
		angry_heart_icon: {
			type: "obj boss_progress_display.angry_heart_icon",
			init: "object('boss_progress_display.angry_heart_icon', mid_x, mid_y, {parent: me})",
		},


		constructor: "[
			show_bar,
			
			set(angry_heart_icon.relative_x, generic_padding_value + angry_heart_icon.img_w/2),
		]",
		
		generic_padding_value: "int :: 16", //used on both sides of the screen, and between the icon and the bar.
		heart_icon_margin: "int :: angry_heart_icon.img_w + generic_padding_value",
		hitpoint_well_length: "int :: level.camera_position[2] - heart_icon_margin - (generic_padding_value * 3)",

		draw_bar: "
			[
				set(hp_bar_left.relative_x, x_offset + bar_inset.x),
				set(hp_bar_left.relative_y, y_offset + bar_inset.y),
				
				set(hp_bar_right.relative_x, x_offset + display_length - bar_inset.x ),  
				set(hp_bar_right.relative_y, y_offset + bar_inset.y),


				set(well_left.relative_x, x_offset),
				set(well_left.relative_y, y_offset),
				
				set(well_right.relative_x, x_offset + well_left.img_w + hitpoint_well_length - well_right.img_w),
				set(well_right.relative_y, y_offset),
				
								
				if(display_length > 8, 
					[
						show_bar,
						hp_bar_center.set_draw_params(
							x_offset + bar_inset.x + hp_bar_left.img_w,
							y_offset + bar_inset.y,
							display_length - 2 * bar_inset.x - hp_bar_right.img_w
						),
						
						well_center.set_draw_params(
							x_offset + well_left.img_w,
							y_offset,
							hitpoint_well_length - well_right.img_w
						),
					],
					hide_bar
				)
			]
				where x_offset = heart_icon_margin + generic_padding_value
				where y_offset = 3
				
				where bar_inset = {x: -4, y: -4} //the bar sits a few pixels inside the well.
			 
				where display_length = if(recent_max_hitpoints > 0, lib.math.ceil( hitpoint_well_length * hitpoint_fraction ), 0)
		",

		show_bar: "[
			add_object(hp_bar_center),
			add_object(hp_bar_left),
			add_object(hp_bar_right),
			add_object(well_center),
			add_object(well_left),
			add_object(well_right),
			add_object(angry_heart_icon),
		]",

		hide_bar: "[
			remove_object(hp_bar_center),
			remove_object(hp_bar_left),
			remove_object(hp_bar_right),
			remove_object(well_center),
			remove_object(well_left),
			remove_object(well_right),
			remove_object(angry_heart_icon),
		]",

#-------------------------- direct values --------------------------#
		
		/*
			'direct' in the sense that half the time, we're doing a cached or time-delayed value.  This isn't that - this is reading the values directly from the "source of truth".
		*/
		direct_boss_max_hp: "fold([int]<-map(level.player.boss_list, value.max_hitpoints), a+b)",
		direct_boss_hp: "fold([int]<-map(level.player.boss_list, value.hitpoints), a+b)",



#-------------------------- tweening/display values --------------------------#
		bar_tween_speed: { type: "decimal", default: 16.0 }, //Larger numbers = slower bar adjustment.
		
		last_displayed_value: { type: "decimal", default: 0.0 },
		recent_max_hitpoints: { type: "decimal", default: 0.0 },


		adjustment_this_frame: "decimal :: (direct_boss_hp - last_displayed_value) / bar_tween_speed",
		
		currently_displayed_value: "decimal :: if(abs(adjustment_this_frame) < 0.01,
			direct_boss_hp,
			last_displayed_value + adjustment_this_frame
		)",
		
		hitpoint_fraction: "decimal :: if(recent_max_hitpoints > 0, decimal(currently_displayed_value) / recent_max_hitpoints, 0.0)",
		
		display_length: "int :: lib.math.ceil( hitpoint_well_length * hitpoint_fraction )",
	},
	
	on_create: "[
		constructor,
	]",
	
	on_being_removed: "hide_bar",
	
//	on_player_death: "reset_boss_list",
	
	on_process: "[
		[
			set(last_displayed_value, currently_displayed_value),
				// cache the hitpoints, because if the boss dies, we can no longer directly access it.
			if(direct_boss_max_hp > 0, set(recent_max_hitpoints, max(recent_max_hitpoints, direct_boss_max_hp))),
		
			if(currently_displayed_value, 
				draw_bar,
				[
					set(recent_max_hitpoints, 0),
					set(level.player.boss_list,[]),
					hide_bar,
				]
			),

		]
	]
	",


	object_type: [{
			"@base": true,
			serializable: false,
			zorder: "@include data/zorder.cfg:in_front_of_everything",
			zsub_order: 2,
			is_strict: true,
			use_absolute_screen_coordinates: true,
		},
		{
			id: "bar_center",
			properties: {
				_length: { type: "int", default: 10 },
				set_draw_params: "def(int new_x, int new_y, int new_length) -> commands [
										set(relative_x, new_x),
										set(relative_y, new_y),
										set(_length, new_length)
									]"
						

			},
			on_process: "set(draw_area, [0.0, 0.0, lib.math.ceil(_length/2.0), img_h/2.0])",
		
			animation: {
				id:	'normal',
				image: 'gui/progress-bar-center.png',
				rect: [0,0,7,7], 
				no_remove_alpha_borders: true,
			},
		},{
			id: "left_cap",

			animation: {
				id:	'normal',
				image: 'gui/progress-bar-endcaps.png',
				rect: [0,0,7,7],
				no_remove_alpha_borders: true,
			},
		},{
			id: "right_cap",

			animation: {
				id:	'normal',
				image: 'gui/progress-bar-endcaps.png',
				rect: [0,8,7,15], 
				no_remove_alpha_borders: true,
			},
		},
		
		
		

		{
			id: "well_center",
			zsub_order: 1,
			properties: {
				_length: { type: "int", default: 10 },
				set_draw_params: "def(int new_x, int new_y, int new_length) -> commands [
										set(relative_x, new_x),
										set(relative_y, new_y),
										set(_length, new_length)
									]"
						

			},
			on_process: "set(draw_area, [0.0, 0.0, lib.math.ceil(_length/2.0), img_h/2.0])",
		
			animation: {
				id:	'normal',
				image: 'gui/progress-bar-well-center.png',
				rect: [0,0,15,15], 
				no_remove_alpha_borders: true,
			},
		},{
			id: "well_left_cap",
			zsub_order: 1,

			animation: {
				id:	'normal',
				image: 'gui/progress-bar-well-endcaps.png',
				rect: [0,0,15,15],
				no_remove_alpha_borders: true,
			},
		},{
			id: "well_right_cap",
			zsub_order: 1,

			animation: {
				id:	'normal',
				image: 'gui/progress-bar-well-endcaps.png',
				rect: [0,16,15,31], 
				no_remove_alpha_borders: true,
			},
		},
		
		
		
		{
			id: "angry_heart_icon",

			animation: {
				id:	'normal',
				image: 'gui/hud.png',
				rect: [101,47,126,72], 
				no_remove_alpha_borders: true,
			},
		},	
	],


}