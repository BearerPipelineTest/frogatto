//The mana bar displays how much energy for abilities we have.
{
	id: "mana_display",
	is_strict: true,
	use_absolute_screen_coordinates: true,
	hidden_in_game: true,
	serializable: false,
	
	zorder: "@include data/zorder.cfg:in_front_of_everything",
	zsub_order: 1,
	no_compile_image: "gui/progress-bar-center.png",

	properties: {
		target: "obj player_controlled <- lp asserting lp where lp = level.local_player",
		
		mana_bar_max_length: "61*2",
		mana_bar_cap_width: 5,
		mana_bar_length: "int :: round(((mana_bar_max_length - mana_bar_cap_width) * target.mana) / target.max_mana)",
	
		mana_bar_container: {
			type: "obj mana_display.bar_container",
			init: "object('mana_display.bar_container', {x: me.x, y: me.y})",
		},
		mana_bar_center: {
			type: "obj mana_display.bar_center",
			init: "object('mana_display.bar_center', x,y,1)",
		},
		mana_bar_top: {
			type: "obj mana_display.top_cap",
			init: "object('mana_display.top_cap', x,y,1)",
		},
		mana_bar_bottom: {
			type: "obj mana_display.bottom_cap",
			init: "object('mana_display.bottom_cap', x,y,1)",
		}
	},
	
	on_start_level: "[
		add_object(mana_bar_container),
		add_object(mana_bar_center),
		add_object(mana_bar_top),
		add_object(mana_bar_bottom),
	]",
	on_create: "[
		add_object(mana_bar_container),
		add_object(mana_bar_center),
		add_object(mana_bar_top),
		add_object(mana_bar_bottom),
	]",
	
	on_process: "[
		[
			set(mana_bar_top.x, x_offset), set(mana_bar_top.y, y_offset),
			set(mana_bar_bottom.x, x_offset ), set(mana_bar_bottom.y, y_offset+ mana_bar_length+4),
			if(mana_bar_length > 5, 
				[set(mana_bar_top.alpha,255), set(mana_bar_bottom.alpha,255), set(mana_bar_center.alpha,255), 
					mana_bar_center.set_draw_params( x_offset, y_offset+10, mana_bar_length-4)],
				[set(mana_bar_top.alpha,0), set(mana_bar_bottom.alpha,0), set(mana_bar_center.alpha,0)]
			)
		] where x_offset = me.x+4 where y_offset = me.y-2, //42
	]",
	
	
	object_type: [{
			"@base": true,
			serializable: false,
			zorder: "@include data/zorder.cfg:in_front_of_everything",
			zsub_order: 2,
			is_strict: true,
			use_absolute_screen_coordinates: true,
		},
		{
			id: "bar_container",	
			zsub_order: 0,
				
			animation: {
				id:	'normal',
				image: 'gui/hud3.png',
				rect: [5,28,26,93], 
			},
		},		
		{
			id: "bar_center",
			properties: {
				_height: { type: "int", default: 10 },
				set_draw_params: "def(int new_x, int new_y, int new_height) -> commands [
										set(x, new_x),
										set(y, new_y),
										set(_height, new_height)
									]"
						

			},
			on_process: "set(draw_area, [0.0, 0.0, img_w/2.0, _height/2.0])",
		
			animation: {
				id:	'normal',
				image: 'gui/mana-bar-center.png',
				rect: [0,0,17,31], 
			},
		},{
			id: "top_cap",

			animation: {
				id:	'normal',
				image: 'gui/hud3.png',
				rect: [0,0,17,7],
			},
		},{
			id: "bottom_cap",

			animation: {
				id:	'normal',
				image: 'gui/hud3.png',
				rect: [0,8,17,15], 
			},
		},
	],
	
}
