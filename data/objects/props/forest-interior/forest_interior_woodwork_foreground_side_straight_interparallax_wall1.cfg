{
id: "forest_interior_woodwork_foreground_side_straight_interparallax_wall1",
zorder: "@include data/zorder.cfg:foreground_parallax_objects",

editor_info: {
	category: "edifice, forest-interior",
},

properties: {
	distance: "decimal :: (screen_midpoint - me.midpoint_x - facing*(img_w * 6/8.0))",
	screen_midpoint: "decimal :: level.camera_position[0] + level.camera_position[2]/2",
	screen_right: "decimal :: level.camera_position[0] + level.camera_position[2]",
	screen_left: "decimal :: level.camera_position[0]",

	is_visible:  "bool ::	(facing = 1 and draw_mid < screen_midpoint) or
							(facing = -1 and draw_mid > screen_midpoint)",


	offset_left: "decimal :: if(facing = 1, -distance*1.0, 0)",
	offset_right: "decimal :: if(facing = -1, -distance*1.0, 0)",
	draw_mid: "decimal :: mid_x + facing*(img_w * 1/8.0)",

	//this is the point where we start the "black fill from here to the edge of the screen"
	cutoff_point: "decimal :: if(facing = 1,
									draw_mid,
									(x2 + offset_right)
								)",
	
	desired_alpha: "int :: lib.math.round( if(is_visible,
												0,
												255 - 255 * (1 - abs(screen_midpoint - cutoff_point)/150)
											))",
	
	set_black_fill_area: "commands :: [
								set(black_fill.x, if(facing = 1,
										x + offset_left,
										screen_left
									)
								),
								set(black_fill.y, y),
								set(black_fill.alpha, desired_alpha),
								set(black_fill.draw_area, [0, 0, bf_width, img_h])
							] where bf_width = if(facing = 1,
													screen_right - draw_mid,
													if((x2 + offset_right) > screen_left, 1 + abs(lib.math.ceil(screen_left - (x2 + offset_right))/2), 0)
												)
							
							",


	black_fill: { type: "obj forest_interior_woodwork_foreground_side_straight_interparallax_wall1.black_fill",  init: "object('forest_interior_woodwork_foreground_side_straight_interparallax_wall1.black_fill',x,y,0)" },

},


on_create: "add_object(black_fill)",

on_process: "([
			set(alpha, desired_alpha),
			set_black_fill_area,
			
			set(custom_draw,
			[0.0,1.0,2.0,3.0,
			[offset_left,0],[offset_right,0],[offset_right,0],[offset_left,0]])
		]
		
		)",



animation: {
	id: "normal",
	image: "props/forest-interior-woodwork5.png",
	rect: [0,0,127,383],
	//palettes: "@include data/palettes.cfg:dungeon_palettes",
},

object_type: {
	id: "black_fill",
	zorder: "@include data/zorder.cfg:foreground_parallax_objects",


	animation: {
		id: "normal",
		image: "props/black-fill.png",
		rect: [0,0,31,31],
	},

}

}