{
id: "periodic_mook_holder_and_spawner",
hidden_in_game: true,
is_strict: true,
prototype: ["periodic_mook_spawner"],

properties: {
		/*
			The object will be 'held' in the spawner for a fixed period of time, and then it will get launched out (either down, or up, as appropriate).
		*/
	release_countdown_length: "int :: 150",

#-------------------------- temporary vars --------------------------#
	_spawning: { type: "obj hittable|null", default: null, persistent: false },
	_release_counter: { type: "int", default: 0, persistent: false },


#-------------------------- behavior settings --------------------------#
	play_spawn_sound: "sound_falloff('ant-spawn.wav')",
	zorder_whilst_spawning: "@include data/zorder.cfg:exterior_near_background",
	spawn_vel: "{x: int, y: int} :: {x: 0, y: -800}",
	spawn_offset: "{x: int, y: int} :: {x: 0, y: 0}",

#-------------------------- behavior --------------------------#
	create_and_hold_child: "commands :: if(_spawning = null, [
			play_spawn_sound,
			set(the_child.animation, 'stand'),
			set(_spawning, (obj hittable <- the_child)),
			add_object(the_child),
		] where the_child = object(spawn_type,
			mid_x + spawn_offset.x,
			mid_y + spawn_offset.y,
		{
			parent: me,
			collides_with_level: false,
			solid_dimensions_in: [],
			zorder:	zorder_whilst_spawning,
		})
	)",
	
	release_child: "commands :: if(_spawning != null, [
		set(_spawning.parent, null),
		set(_spawning.collides_with_level, true),
		set(_spawning, null),
		set(_spawning.velocity_x, spawn_vel.x),
		set(_spawning.velocity_y, spawn_vel.y),
		set(_spawning.zorder,0),
		set(_spawning.solid_dimensions_in, _spawning.default_solid_dimensions),
		set(_release_counter, 0)
	])",
	
	

},


on_process: "[
		if(_spawning, set(_spawning.accel_y, 0)),
		if(_mook_tracker.should_create_a_new_mook or _spawning != null, add(_release_counter, 1), set(_release_counter, 0)),
		if(_release_counter >= (0.75 * release_countdown_length), create_and_hold_child ),
		if(_release_counter >= (1.00 * release_countdown_length), release_child)
	]",

}
